plugins {
    id 'org.labkey.build.module'
}

ext.luceneVersion="8.11.0"
ext.mime4jVersion="0.8.4"

dependencies {
    external "org.apache.lucene:lucene-analyzers-common:${luceneVersion}"
    external "org.apache.lucene:lucene-core:${luceneVersion}"
    external "org.apache.lucene:lucene-queryparser:${luceneVersion}"

    // tika-parsers.jar includes some simple text parsers plus adapters for many parsing libraries
    external ("org.apache.tika:tika-parsers-standard-package:${tikaVersion}") {
        transitive = false
    }
    // Additional parsing libraries (beyond POI, PDFBox, etc., which are already included in API) that we
    // care about. Adjust these dependencies and versions whenever we upgrade Tika. Or if we want to add or remove a parser.
    // See also tikaConfig.xml to exclude specific parsers or detectors.
    external "org.apache.commons:commons-compress:${commonsCompressVersion}"
    external "org.tukaani:xz:${tukaaniXZVersion}"         // Transitive dependency of commons-compress -- Issue 38298: Errors when indexing .7z files in search
    external "org.apache.pdfbox:jempbox:1.8.16"
    external "com.drewnoakes:metadata-extractor:2.16.0"
    external "org.apache.poi:poi-scratchpad:${poiVersion}"
    external "org.ccil.cowan.tagsoup:tagsoup:1.2.1"
    external "org.codelibs:jhighlight:1.0.3"
    external "org.apache.commons:commons-csv:1.9.0"
    external "org.apache.james:apache-mime4j-core:${mime4jVersion}"
    external "org.apache.james:apache-mime4j-dom:${mime4jVersion}"
    external "org.apache.pdfbox:pdfbox:${pdfboxVersion}"
}

// TODO move resources files into resources directory to avoid this overlap
sourceSets {
    main {
        resources {
            srcDirs = ['src'] // src is included because it contains some xml and text files
            exclude '**/*.java'
            exclude '**/*.jsp'
        }
    }
}
